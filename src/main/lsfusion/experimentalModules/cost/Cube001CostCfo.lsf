MODULE Cube001CostCfo;

REQUIRE CalcCore,Calc,DimensCFO,DimensFirm,DimensCostItems,Cube;

NAMESPACE Cube;

// объявление куба
valueCostCfoNum 'Затраты' = DATA NUMERIC[18,2] (Calc, Task, DimFirm, DimCFO, DimCostItems, Period);

// итог по строке
sumByRow(Calc c,Task cc,DimFirm firm, DimCFO d1,DimCostItems d2 ,Period p) = 
    GROUP SUM valueCostCfoNum (Calc c_,Task cc_,DimFirm firm_ ,d1,d2,Period p_) IF c == c_ AND cc==cc_ AND firm == firm_;
 
// расчет итога по группам
WHEN LOCAL CHANGED (valueCostCfoNum (Calc c,Task cc, DimFirm firmRow, DimCFO d1,DimCostItems d2 ,Period p))
 DO 
 {
// 2 уровень ОБЩИЙ ИТОГ  по затратам   
    valueCostCfoNum(c,cc,firmRow,d1, (GROUP MAX  DimCostItems i AS DimCostItems IF isRoot(i)),p)
    <- 
    GROUP SUM valueCostCfoNum(c,cc,firmRow,d1,DimCostItems d, p) IF NOT (isGroup(d) OR isRoot(d));

    FOR isGroup(DimCostItems i)  DO {
        valueCostCfoNum(c,cc,firmRow,d1, i, p) 
        <- 
        GROUP SUM valueCostCfoNum(c,cc,firmRow,d1,DimCostItems d, p) 
            IF (NOT (isGroup(d) OR isRoot(d))) AND isParent(d,i);
    }
 
   //1 уровень
    valueCostCfoNum(c,cc,firmRow, (GROUP MAX  DimCFO  i1 AS DimCFO IF isRoot(i1)),
                          (GROUP MAX  DimCostItems i AS DimCostItems IF isRoot(i)),
                          p
                   )
    <- 
    GROUP SUM valueCostCfoNum(c,cc,firmRow, DimCFO dCfo,DimCostItems d, p) 
        IF NOT (isGroup(d) OR isRoot(d) OR isGroup(dCfo) OR isRoot(dCfo));

    FOR isGroup(DimCFO i) AND isParent(d1,i) DO {
        valueCostCfoNum(c,cc,firmRow,i,(GROUP MAX  DimCostItems iCost AS DimCostItems IF isRoot(iCost)), p) 
        <- 
        GROUP SUM valueCostCfoNum(c,cc,firmRow,DimCFO ii  ,DimCostItems d, p) 
            IF (NOT (isGroup(d) OR isRoot(d) OR isGroup(ii) OR isRoot(ii))) AND isParent(ii,i);
    }
 }
 