MODULE Calc;

REQUIRE TaskTune, Utils, Time, PeriodCalendar, DimensFirm;

NAMESPACE Calc;


// --- шапка расчета объединяющая книги --------------------
CLASS Calc  'Расчет';
TABLE calc(Calc);

// 1. ссылка на родительский расчет... 
// расчет  = что если... в принципе это копия расчета с полным пересчетом
// копия расчета - имеет номер копии с одной стороны. 
//       ветвится от расчета - основной расчет = родитель /
// карта - дерево расчета - последовательность расчета
// пометка -  изменение данных -  необходимость считать копию
// 
parentCalc '' = DATA Calc(Calc);
calcVersionNum = DATA INTEGER (Calc);
readOnlyHeader '' = DATA BOOLEAN (Calc);

calcDate 'Дата расчета' = DATA DATE (Calc) IN base NONULL ; 
calcDateBegin 'начало'  = DATA DATE (Calc) IN base NONULL; 
calcDateEnd 'окончание' = DATA DATE (Calc) IN base NONULL; 

dimFirm     'Организация' = DATA DimFirm(Calc);

description 'Комментарий' = DATA STRING[1000] (Calc) IN base;
periodType 'Используемые периоды'  =  DATA PeriodType (Calc) NONULL;
scenario 'Сценарий' = DATA Scenario(Calc ) NONULL;
calcVariant 'Вид расчета'  = DATA CalcVariant(Calc) NONULL;

// Характеристики бюджета используемые между ордерами
paramProportionPart     'Доля в днях'                         = DATA INTEGER (Calc);
paramProportionWhole    'Полная доля (рабочих дней)'          = DATA INTEGER (Calc);

isTheSameScenario(Calc c1,Calc c2) =  scenario(c1) == scenario(c2) AND dimFirm(c1) == dimFirm(c2) ;
isTheSameScenario(Calc c1,Calc c2, Scenario s2) =  s2 == scenario(c1) AND dimFirm(c1) == dimFirm(c2) ;
isTheSameScenario(Calc c1, DimFirm dimFirm, Scenario s2) =  s2 == scenario(c1) AND dimFirm(c1) == dimFirm;

periodInCalc(Period p, Calc calc) = GROUP LAST PeriodCalendar c AS PeriodCalendar IF dateBegin(c) >= calcDateBegin(calc)  
                                          AND dateEnd(c) <= calcDateEnd(calc) AND periodType(c) = periodType(p)
                                          ORDER pos(period(c));

periodInCalc(PeriodCalendar p, Calc calc) = dateBegin(p) >= calcDateBegin(calc) AND dateEnd(p) <= calcDateEnd(calc) AND periodType(calc) = periodType(p);
periodInCalc(PeriodCalendar p, Calc calc, DATE d1, DATE d2) = 
                                                    dateBegin(p) >= calcDateBegin(calc) 
                                                AND dateEnd(p)   <= calcDateEnd(calc) 
                                                AND periodType(calc) = periodType(p)
                                                AND d1 <= dateBegin(p) AND d2 >= dateEnd(p);
 


name 'Наименование' (Calc c)   =  STRING[80] (CONCAT  ', ', nameCalcVariant(calcVariant(c)),
                                        nameScenario(scenario(c)),   
                                        nameType(periodType(c)),
                                        (
                                        CONCAT  ' ',
                                            'с ',
                                           toChar(calcDateBegin(c),'DD.MM.YYYY'),
                                            'по ',
                                           toChar(calcDateEnd(c),'DD.MM.YYYY') 
                                        )   )                   
                                        ;// DATA STRING[254] (Calc) IN id;
                                        
  
getPeriodByDate(Calc c,DATE d) =  GROUP MAX period(PeriodCalendar p) IF periodType(p) == periodType (c) 
                                                                                AND d >= dateBegin(p) 
                                                                                AND d<= dateEnd(p);
 
openCalcCard ABSTRACT (Calc);
 
readOnlyTitleCalc(Calc c) = calcVersionNum(c) != NULL; 

FORM calcList 'Реестр расчетов' 
OBJECTS c = Calc LAST 
PROPERTIES opn 'Открыть' = openCalcCard(c) GRID IMAGE 'images/folder_table.png'   
PROPERTIES   READONLY   
            n01 'Дата' = calcDate(c),
            n03 'Сценарий' = nameScenario(scenario(c))  ,
            nameCV 'Вид расчета' = nameCalcVariant(calcVariant(c))  ,
            begDate 'с'  = calcDateBegin(c), 
            endDate 'по' = calcDateEnd(c),
            dimFirm 'Организация' = name(dimFirm(c)),
            n02 'Комментарий' = description(c)   
PROPERTIES(c) NEW,DELETE
FILTERS hasCurrentUserDimFirmPermission(dimFirm(c)) OR NOT dimFirm(c)
ORDERS n01,n03
 ;

DESIGN calcList {
      BOX (c) {caption = NULL; collapsible = FALSE;}
      PROPERTY(n01) { charWidth=14; } 
      PROPERTY(n03) { charWidth=10; }
      PROPERTY(nameCV) { charWidth=10; }
      PROPERTY(begDate) { charWidth=14; } 
      PROPERTY(endDate) { charWidth=14; } 
      PROPERTY(dimFirm) { charWidth=10; } 
      PROPERTY(n02) { charWidth=50; } 
};
 
FORM calcListSelect 'Реестр расчетов' 
OBJECTS c = Calc LAST 
PROPERTIES  READONLY 
            n01 'Дата' = calcDate(c),
            n03 'Сценарий' = nameScenario(scenario(c))  ,
            nameCV 'Вид расчета' = nameCalcVariant(calcVariant(c))  ,
            begDate 'с'  = calcDateBegin(c), 
            endDate 'по' = calcDateEnd(c),
            dimFirm 'Организация' = name(dimFirm(c)),
            n02 'Комментарий' = description(c)   
ORDERS n01,n03
LIST Calc OBJECT c;



DESIGN calcListSelect {
      PROPERTY(n01) { charWidth=16; } 
      PROPERTY(n03) { charWidth=10; }
      PROPERTY(nameCV) { charWidth=10; }
      PROPERTY(begDate) { charWidth=14; } 
      PROPERTY(endDate) { charWidth=14; } 
      PROPERTY(dimFirm) { charWidth=10; } 
      PROPERTY(n02) { charWidth=50; } 
};
  
  
 openTask ABSTRACT (Calc, Task);
 
FORM  reportListExecCalc 'Список отчетов'
OBJECTS  calc = Calc PANEL; // предварительное объявление формы выбора отчета

// ================ОСНОВНАЯ ФОРМА================================================
           
FORM calcCard 'Расчет' 
OBJECTS curCalc  = Calc PANEL 
//OBJECTS p = CalcPeriod
OBJECTS task = Task 
PROPERTIES READONLYIF readOnlyHeader(curCalc)
           firm 'Организация' = name(dimFirm(curCalc)),
           n01 'Дата' = calcDate (curCalc) , 
           n04 'Вид расчета' = nameCalcVariant(calcVariant(curCalc)) NOSELECT,
           n03 'Сценарий' = nameScenario(scenario(curCalc)),
          
           n1 'Периодичность' = nameType(periodType(curCalc)),
           begDate 'с'  = calcDateBegin(curCalc),  
           endDate 'по' = calcDateEnd(curCalc)
PROPERTIES n02 'Комментарий' = description(curCalc), 
           showReportList 'Отчеты'= {SHOW reportListExecCalc OBJECTS calc = curCalc WAIT FLOAT;},
           ro '' = readOnlyHeader(curCalc)
EVENTS ON APPLY BEFORE {readOnlyHeader(curCalc) <- TRUE;}
      
PROPERTIES  ON CHANGE { openTask(curCalc, task); }
            openTask(curCalc, task) GRID READONLYIF isGroup(task) IMAGE IF isGroup(task)  THEN 'images/table_multiple.png' ELSE 'images/table_edit.png',  
            nameTaskTab(task) FOREGROUND  IF isGroup(task) THEN Budget.groupFontColor(),
            nameCol 'тип' = nameCalcBook(type(task)), 
            canonicalName(task), pos(task) // hide

FILTERS calcVariant(curCalc) == calcVariant(task)
FILTERS hasCurrentUserTaskPermission(task)  
ORDERS  pos(task), canonicalName(task)
;
  
  
// открываем форму расчета с деревом задач
openCalcCard (Calc c_) + {
   SHOW calcCard OBJECTS  curCalc = c_ DOCKED NOWAIT MANAGESESSION;
} 
 
 

  
DESIGN calcCard   { //nameTask
caption =    nameScenario(scenario(curCalc)) + ': ' + toDateDDMMYYYY( calcDateBegin(curCalc)) ;

    PROPERTY (nameTaskTab(task)) {charWidth = 60;}
    PROPERTY (nameCol) {charWidth = 40;}
    PROPERTY (pos(task)) {hide =TRUE;}
    PROPERTY (canonicalName(task)){hide=TRUE;}
    PROPERTY (pos(task)){hide=TRUE;}
 
  PANEL (curCalc) {
        caption = NULL;
        NEW pan1 {
            lines = 10;
            MOVE PROPERTY(firm) { charWidth=14; align = STRETCH;};
            MOVE PROPERTY(n01) { charWidth=14; align = STRETCH;};
            MOVE PROPERTY(n04) { charWidth=14; align = STRETCH;};
            MOVE PROPERTY(n03) { charWidth=14; align = STRETCH;};
        }
        NEW pan2 {
            lines = 10;
            MOVE PROPERTY (showReportList) {background = #edfffc;}; 
            MOVE PROPERTY(begDate) { charWidth=14; align = STRETCH;} 
            MOVE PROPERTY(endDate) { charWidth=14; align = STRETCH;} 
            MOVE PROPERTY(n1) { charWidth=14; align = STRETCH;}
            MOVE PROPERTY(n02) { charWidth = 50; align = STRETCH;}
            MOVE PROPERTY(ro); 
            
        }
    
        
  }
   OBJECTS {
      NEW tabPanelcalc {
                 horizontal = TRUE;
                 flex = 1; 
                 align  = STRETCH;
                 MOVE   BOX (task); 
            }
    }
};
  