MODULE TaskTune;

REQUIRE Task, TaskPermission;

NAMESPACE MyBalance;

// на все элементы посадим описание, типы форм, функции, отчеты, импорты/экспорты
descrMyBalance           'Описание элемента'       (Object d)     = ABSTRACT CASE TEXT; //TypeRequest, MenuOperations*, TypeTask
descrMenuOperations      'Описание операций'       (Task d)       = ABSTRACT CASE TEXT; // возв
descrSectionRequest      'Описание Ипорта/экспорта'(TypeTask d)   = ABSTRACT CASE TEXT; 


 

EXTEND FORM taskListCard
PROPERTIES 
description          'Описание задачи'  =  descriptionTask(f),
descriptionTypeTask  'Описание   типа'  =  descrMyBalance(type(f));

DESIGN taskListCard {
    OBJECTS {
        NEW description {
            fill = 1;
            MOVE PROPERTY (description) {height = 200;} ;
            MOVE PROPERTY (descriptionTypeTask) ;
        }
    }
}



FORM taskTune 'Настройка задач расчета'
OBJECTS cv = CalcVariant  PANEL
TREE taskTree f = Task PARENT parent(f) 
PROPERTIES nameCalcVariant(cv) READONLY PANEL  
PROPERTIES  ON CHANGE {SHOW taskListCard;} 
            pos(f),
            nameTaskTab(f)  
                FOREGROUND  IF isGroup(f) THEN groupFontColor(), 
                
                
            nameCol 'тип' = nameCalcBook(type(f))  ,
            parentCol 'в группе' = nameTask(parent(f))  ,
            isGroup(f)

PROPERTIES  stButN 'Новый элемент' = NEW(f), stButD 'Удалить' = DELETE(f)

EVENTS ON INIT {EXPAND ALL TOP taskTune.f;}
// EVENTS ON CHANGE f {EXPAND ALL calcCompositionTune.f;}
FILTERS cv = calcVariant(f)

OBJECTS ur = UserRole
PROPERTIES taskPermission(f , ur)
PROPERTIES(ur) READONLY sid, name


OBJECTS scenario = Scenario
PROPERTIES enableScenario(f , scenario)
    ON CONTEXTMENU 'Изменить для всех задач этого вида расчета' {
        INPUT  inp = enableScenario(f , scenario) DO  {
            FOR cv == calcVariant(Task taskAll) DO {
                enableScenario(taskAll , scenario) <- inp;
            }
        }
    }
    
PROPERTIES(scenario) READONLY nameScenario
;


//
newGroupTask(CalcVariant cv) {
  NEW new = Task {
        isGroup(new) <-TRUE;
        nameTask(new) <- 'Новая группа';
        calcVariant(new) <- cv;
        SEEK taskTune.f = new;
    }
}
EXTEND FORM taskTune 
PROPERTIES newGroup '+ Новая группа' = newGroupTask(cv);

// открытие формы настроек сделаем из списка расчетов
openTaskList(CalcVariant calcV) + {
    SHOW taskTune OBJECTS  cv = calcV DOCKED NOWAIT MANAGESESSION 
    ;
}


DESIGN  taskTune {
    PROPERTY (isGroup(f)){ hide = TRUE; }
    PROPERTY (nameTaskTab(f)) { charWidth =20; } 
    PROPERTY (parentCol) {charWidth = 20;} 
    PROPERTY (nameCalcVariant(cv) ) { fontStyle = 'bold'; } 
     
   TOOLBAR ( TREE taskTree) {
        MOVE PROPERTY (newGroup)  BEFORE PROPERTY (stButN);
   }
   OBJECTS {
    NEW mainPanel{
        fill = 1;

        MOVE  BOX(TREE taskTree) {fill = 2;};
        NEW panelBt{
            fill = 1;
            tabbed = TRUE;
            horizontal = TRUE;
            lines = 1;
            MOVE BOX (ur) {fill = 0.5; caption = 'Права доступа';}
            MOVE BOX (scenario) {fill = 0.5; caption = 'Видимость в сценариях';}
            NEW details  {
                caption = 'Преднастр.значений'; 
                // создаем новый контейнер в стандартный контейнер сразу после i.box
                // этот контейнер будет панелью закладок, в который можно будет добавлять закладки со свойствами товара
            tabbed = TRUE; 
            fill = 0.5;  
            }
      }
    }
  }     
};




