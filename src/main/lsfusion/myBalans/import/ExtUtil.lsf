MODULE ExtUtil;
 
REQUIRE Utils,Time;

NAMESPACE MyBalance;

CLASS ABSTRACT LocalVars; 

// ОФОРМЛЕНИЕ
htmlBold(STRING s) =  '<b>'+s+'</b>';
htmlColor(STRING s, STRING colorString) =    '<font color="'+colorString+'">'
    + s
    + '</font>';
;

// СТРОКИ
trimDeltn(STRING  s) = regexpReplace(trim(replace(replace(s,'\n',' '),'\t',' ')),'( ){2,}', ' ',''); //   без перевода строк, табуляций и двойных пробелов
 
substrFromRegExp (TEXT p1, STRING p2) = FORMULA TEXT PG 'substring($1, $2)';
textValue2JsonFile(TEXT t) = IF  left(t,1) == '['  THEN   FILE(CSVFILE ('\{ "value":' + t + ' \}')) ELSE  FILE(CSVFILE ('\{ "value":[' + t + '] \}')) ;

stringToLenLeftSym (STRING st, INTEGER l, STRING ins) =  STRING (repeat(ins, MAX 0,(l - length(st))) + st);
stringToLenLeft 'Дополнить слева' (STRING st, INTEGER l) =   stringToLenLeftSym(st, l, '0') ;


//ДАты
countMonthExcl(DATE d1, DATE d2) =  extractYear(d2) - extractYear(d1) + extractMonthNumber(d2) - extractMonthNumber(d1);
mb_daysInclBetweenDates(DATE dateMax, DATE dateMin) = daysBetweenDates(dateMax, dateMin) +1;


// ДЛЯ 1с
to1cDate0000(DATE d) = toChar(d,'YYYY-MM-DD')+'T00:00:00';
to1cDate2359(DATE d) = toChar(d,'YYYY-MM-DD')+'T23:59:59';
dateTimeTo1cDate(DATETIME d) =  toChar(d,'YYYY-MM-DD')+'T'+toChar(d,'HH24:MI:SS');
    
date1cToDateTime(STRING s) = toDateTimeFormat(s,'YYYY-MM-DDTHH24:MI:SS');
date1cToDate(STRING s) = DATE(date1cToDateTime(s));

docNumberFrom1cPr(STRING s) =  substrFromRegExp(s,'([^[:space:]]+)(?= от)');
docDateFrom1cPr(STRING s) =   toDateFormat(substrFromRegExp(s,'[0-3]\\d\\.[0-1]\\d\\.[1-2]\\d\\d\\d'),'DD.MM.YYYY');

//   Начиная с PostgreSQL 15
regexpSubstr (STRING string, STRING regexp, INTEGER startPosition, INTEGER position) = FORMULA STRING 'regexp_substr($1,$2,$3,$4,\'\',1)';
regexpCount  (STRING string, STRING regexp) = FORMULA INTEGER 'regexp_count($1,$2)';
 
// ФОРМЫ вспомогательные
extDataDescription = DATA LOCAL ISTRING[100] (INTEGER) DEFAULTCOMPARE '=*' IN id;

FORM formXlsxLoad 'Выбор файла xlsx'
    PROPERTIES ff 'Загрузите файл нажав на поле' = rawFile();
DESIGN formXlsxLoad {
    size  = (400,200);
    PROPERTY(ff) {charWidth  = 12;}
}


FORM formExtDataSelect 'Выбор элемента' 
OBJECTS  o = INTEGER 
PROPERTIES READONLY extDataDescription(o)
FILTERS extDataDescription(o);
 
formInteger = DATA LOCAL INTEGER ();
formDateFromTo1 = DATA LOCAL NESTED DATE ();
formDateFromTo2 = DATA LOCAL NESTED DATE ();
formDateFromTo1caption = DATA LOCAL STRING[50]();
formDateFromTo2caption = DATA LOCAL STRING[50]();

FORM formDateFromTo 'Выбор периода'
PROPERTIES PANEL 
d1  = formDateFromTo1(),
d2 'по'     = formDateFromTo2(); 

DESIGN formDateFromTo{
    horizontal = FALSE;
    lines = 1;
    PROPERTY (d1 ) {caption = OVERRIDE formDateFromTo1caption(),'Дата с';}
    PROPERTY (d2 ) {caption = OVERRIDE formDateFromTo2caption(),'по';}
}
 
FORM formDateTime 'Дата с временем'
OBJECTS d = (d1 = DATETIME) PANEL 
PROPERTIES 
d1 'Время: ' = VALUE (d1)
;  
 
numberMonthByRusName (STRING pn) = CASE
    WHEN lower(trim(pn)) == 'январь'  THEN 1
    WHEN lower(trim(pn)) == 'февраль'  THEN 2
    WHEN lower(trim(pn)) == 'март'  THEN 3
    WHEN lower(trim(pn)) == 'апрель'  THEN 4
    WHEN lower(trim(pn)) == 'май'  THEN 5
    WHEN lower(trim(pn)) == 'июнь'  THEN 6
    WHEN lower(trim(pn)) == 'июль'  THEN 7
    WHEN lower(trim(pn)) == 'август'  THEN 8
    WHEN lower(trim(pn)) == 'сентябрь'  THEN 9
    WHEN lower(trim(pn)) == 'октябрь'  THEN 10
    WHEN lower(trim(pn)) == 'ноябрь'  THEN 11
    WHEN lower(trim(pn)) == 'декабрь'  THEN 12 ;

getLastDateByNameMonth(DATE d, STRING pn) =  GROUP MAX DATE dt IF iterate(dt, sumMonth(d, -12), d) AND extractDay(dt) == 1 AND 
                                                          extractMonthNumber(dt) >= numberMonthByRusName(pn);


 
META addMenuImportExport(formName, ImportProcDescription, typeTask) 
EXTEND CLASS SectionRequest {
    section###formName  ImportProcDescription
}

descrSectionRequest(TypeTask t) += WHEN t = typeTask THEN
    CONCAT '',
        '\n ИМПОРТ/ЭКСПОРТ ФОРМЫ:\n',
         extRequestDescription(SectionRequest.section###formName),   
        '\n===========================\n';

functionsImport###formName  ABSTRACT (CalcTask,ExtRequest);

runActions###formName(CalcTask calcTask_) {
  DIALOG extRequestSelectList OBJECTS  sr = SectionRequest.section###formName, firm = dimFirm(calc(calcTask_)), er INPUT extReq_ FLOAT CANCEL DO {
            functionsImport###formName(calcTask_, extReq_);
     }  
}; 
///3 ОФОРМЛЕНИЕ
EXTEND FORM formName 
PROPERTIES exec 'Импорт/экспорт...' = runActions###formName(calcTask);
 
// ДИЗАЙН
DESIGN formName {
 OBJECTS {
    panelFirstHorizontal   {
      horizontal = TRUE;
      MOVE PROPERTY (exec) FIRST;
      }
    };
 }
END

META  addFunctionImportExport(formName, procedureName, ImportProcDescription) 
EXTEND CLASS TypeRequest {
    formName###procedureName ImportProcDescription
}
procedureName ImportProcDescription (ExtRequest er,CalcTask ct) ABSTRACT;
// ПРОЦЕДУРА ВЫБОРА
functionsImport###formName(CalcTask ct,ExtRequest er) + {
    // берем отчет - грузим отчет
   CASE EXCLUSIVE  
    WHEN type(er) == TypeRequest.formName###procedureName THEN { 
       procedureName(er,ct);
    } 
}
END 


GROUP odata_error EXTID 'odata.error';
GROUP message : odata_error; 
odata_error_code = DATA LOCAL STRING();
odata_error_lang = DATA LOCAL STRING();
odata_error_value = DATA LOCAL STRING();

FORM odata_error 
PROPERTIES() IN odata_error EXTID 'odata.error'
    odata_error_code EXTID 'code'
PROPERTIES() IN message EXTID 'message'
    odata_error_lang EXTID 'lang', 
    odata_error_value EXTID 'value'; 
DESIGN odata_error {PROPERTY (odata_error_value()){charWidth = 150;} }


col1ListFromExcel = DATA LOCAL STRING (INTEGER);
col2ListFromExcel = DATA LOCAL STRING (INTEGER);
col3ListFromExcel = DATA LOCAL STRING (INTEGER);

loadListFromExcel() {
     INPUT f = EXCELFILE DO { // запрашиваем диалог по выбору файла
            open(f); // открываем выбранный файл
            IMPORT XLS NOHEADER FROM f TO col1ListFromExcel = A, col2ListFromExcel = B, col3ListFromExcel = C;
     }
}