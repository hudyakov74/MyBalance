MODULE ExtUtil;
 
REQUIRE Utils,Time;

NAMESPACE ExtSystem;
 
substrFromRegExp (TEXT p1, STRING p2) = FORMULA TEXT PG 'substring($1, $2)'; 
mb_daysInclBetweenDates(DATE dateMax, DATE dateMin) = daysBetweenDates(dateMax, dateMin) +1;
//DATE_PART('day', end - start)

to1cDate0000(DATE d) = toChar(d,'YYYY-MM-DD')+'T00:00:00';
to1cDate2359(DATE d) = toChar(d,'YYYY-MM-DD')+'T23:59:59';
dateTimeTo1cDate(DATETIME d) =  toChar(d,'YYYY-MM-DD')+'T'+toChar(d,'HH24:MI:SS');
    
date1cToDateTime(STRING s) = toDateTimeFormat(s,'YYYY-MM-DDTHH24:MI:SS');
date1cToDate(STRING s) = DATE(date1cToDateTime(s));

textValue2JsonFile(TEXT t) = IF  left(t,1) == '['  THEN   FILE(CSVFILE ('\{ "value":' + t + ' \}')) ELSE  FILE(CSVFILE ('\{ "value":[' + t + '] \}')) ;

docNumberFrom1cPr(STRING s) =  substrFromRegExp(s,'([^[:space:]]+)(?= от)');
docDateFrom1cPr(STRING s) =   toDateFormat(substrFromRegExp(s,'[0-3]\\d\\.[0-1]\\d\\.[1-2]\\d\\d\\d'),'DD.MM.YYYY');

stringToLenLeftSym (STRING st, INTEGER l, STRING ins) =  STRING (repeat(ins, MAX 0,(l - length(st))) + st);
stringToLenLeft 'Дополнить слева' (STRING st, INTEGER l) =   stringToLenLeftSym(st, l, '0') ;
//CLASS ExtImportDataTmp;
//description = DATA LOCAL BPSTRING[100] (ExtImportDataTmp) DEFAULTCOMPARE 'CONTAINS' IN id;
extDataDescription = DATA LOCAL ISTRING[100] (INTEGER) DEFAULTCOMPARE 'CONTAINS' IN id;

FORM formExtDataSelect 'Выбор элемента' 
OBJECTS  o = INTEGER 
PROPERTIES READONLY extDataDescription(o)
FILTERS extDataDescription(o);
 
formDateFromTo1 = DATA LOCAL DATE ();
formDateFromTo2 = DATA LOCAL DATE ();
FORM formDateFromTo 'Выбор периода'
PROPERTIES PANEL 
d1 'Дата с' = formDateFromTo1(),
d2 'по' = formDateFromTo2(); 
 
FORM formDateTime 'Дата с временем'
OBJECTS d = (d1 = DATETIME) PANEL 
PROPERTIES 
d1 'Время: ' = VALUE (d1)
;  
 
 
META addMenuImportExport(formName, ImportProcDescription) 
EXTEND CLASS SectionRequest {
    section###formName  ImportProcDescription
}

functionsImport###formName  ABSTRACT (CalcTask,ExtRequest);

runActions###formName(CalcTask calcTask) {
  DIALOG extRequestSelectList OBJECTS  sr = SectionRequest.section###formName, firm = dimFirm(calc(calcTask)),er INPUT extReq FLOAT CANCEL DO {
            functionsImport###formName(calcTask,extReq);
     }  
}; 
///3 ОФОРМЛЕНИЕ
EXTEND FORM formName 
PROPERTIES exec 'Импорт/экспорт...' = runActions###formName(calcTask);
 
// ДИЗАЙН
DESIGN formName {
 OBJECTS {
    NEW horizPanel FIRST {
      horizontal = TRUE;
      MOVE PROPERTY (exec) FIRST;
      }
    };
 }
END

META  addFunctionImportExport(formName, procedureName, ImportProcDescription) 
EXTEND CLASS TypeRequest {
    formName###procedureName ImportProcDescription
}
procedureName ImportProcDescription (ExtRequest er,CalcTask ct) ABSTRACT;
// ПРОЦЕДУРА ВЫБОРА
functionsImport###formName(CalcTask ct,ExtRequest er) + {
    // берем отчет - грузим отчет
   CASE EXCLUSIVE  
    WHEN type(er) == TypeRequest.formName###procedureName THEN { 
       procedureName(er,ct);
    } 
}
END 


GROUP odata_error EXTID 'odata.error';
GROUP message : odata_error; 
odata_error_code = DATA LOCAL STRING();
odata_error_lang = DATA LOCAL STRING();
odata_error_value = DATA LOCAL STRING();

FORM odata_error 
PROPERTIES() IN odata_error EXTID 'odata.error'
    odata_error_code EXTID 'code'
PROPERTIES() IN message EXTID 'message'
    odata_error_lang EXTID 'lang', 
    odata_error_value EXTID 'value'; 
DESIGN odata_error {PROPERTY (odata_error_value()){charWidth = 150;} }

