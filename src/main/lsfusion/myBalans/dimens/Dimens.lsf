MODULE Dimens;
REQUIRE CoreColor, CoreMenu, Utils, ExtUtil, DimCategory, Reflection, BReportPartition, Security;
NAMESPACE MyBalance;

CLASS DimRegisters {
    empty 'Пусто'
}
nameDim = DATA STRING[100](DimRegisters);
name(DimRegisters d) = OVERRIDE nameDim(d), staticCaption(d);

constructFilter(Object obj, BReportPartitionFilter c) ABSTRACT CASE;
// listBreportSelected(,)

 
META configDim(ObjectDim) 
 // элемент root - представляет собой элемент множества подставляющийся вместо всего множества
 // в формах списков можно не показывать 
     getRoot##ObjectDim()  = GROUP MAX ObjectDim c IF isRoot(c) AND NOT isGroup(c);
     getEmpty##ObjectDim()  = GROUP MAX ObjectDim c IF isRoot(c) AND isGroup(c);
     WHEN LOCAL CHANGED(isRoot(ObjectDim sd))  AND ( (GROUP SUM(1) IF (isRoot(ObjectDim all) AND NOT isGroup(all) AND all IS ObjectDim)) > 1 ) DO {
        isRoot(sd)<-NULL ;
        MESSAGE 'только один элемент может представлять корень';
     };
     // список для выбора группы 
      EXTEND FORM form##ObjectDim##Group  
      OBJECTS d = ObjectDim 
      PROPERTIES(d) READONLY 
      ON CONTEXTMENU 'скрыть элемент вкл/выкл' {isHide(d) <- NOT isHide(d);}
      BACKGROUND  #e3e3e3 IF isHide(d)
      pos, code, canonicalName
      ORDERS pos(d), canonicalName(d), isGroup(d) DESC FILTERS isGroup(d) AND NOT isRoot(d)
      FILTERGROUP hided FILTER 'Скрывать неиспользуемые' NOT isHide(d) DEFAULT;
     
      selectGroup(ObjectDim r) {
          DIALOG form##ObjectDim##Group OBJECTS d = parent(r) CHANGE;
      };
     
  
    EXTEND  FORM  form##ObjectDim##TreeList  
    TREE tree##ObjectDim t = ObjectDim  PARENT parent(t)
    PROPERTIES (t) 
    ON CONTEXTMENU 'скрыть элемент вкл/выкл' {isHide(t) <- NOT isHide(t);}
    BACKGROUND  #e3e3e3 IF isHide(t)
            tabName BACKGROUND color(t),
            name    BACKGROUND color(t),
            pos
    EVENTS ON INIT { EXPAND ALL TOP form##ObjectDim##TreeList.t; }
    FILTERS NOT isRoot(t)
    ORDERS  pos(t),name(t)
    FILTERGROUP hided FILTER 'Скрывать неиспользуемые' NOT isHide(t) DEFAULT
    LIST ObjectDim OBJECT t;
    DESIGN   form##ObjectDim##TreeList { 
        PROPERTY (tabName(t)) { charWidth=100; charHeight = 1; }
    };
  
   // форма для редактирования списка - открывается только из меню
    EXTEND FORM  form##ObjectDim##list  
    OBJECTS d = ObjectDim 
    PROPERTIES(d)   
    ON CONTEXTMENU 'скрыть элемент вкл/выкл' {isHide(d) <- NOT isHide(d);}
    BACKGROUND  #e3e3e3 IF isHide(d)
    isRoot,NEW ,DELETE,
                    isGroup BACKGROUND color(d),
                    levelNum BACKGROUND color(d), 
                   
                    pos BACKGROUND color(d), 
                    tabName BACKGROUND color(d), 
                    name BACKGROUND color(d) 
                    

    PROPERTIES      parent 'Родитель' = name(parent(d)) ON CHANGE selectGroup(d) BACKGROUND color(d),
                    canonicalName(d) BACKGROUND color(d)
    ORDERS  pos(d), canonicalName(d), isGroup(d) DESC
    FILTERGROUP hided FILTER 'Скрывать неиспользуемые' NOT isHide(d) DEFAULT
    ; 
 END


META configDim2TaskTune(ObjectDim,checked,var) 
    
    checked##Hr(Task t, ObjectDim d) = GROUP MAX checked(t, ObjectDim chld) IF isParent(chld, d);
   
    EXTEND FORM  taskTune
    OBJECTS var = ObjectDim
    PROPERTIES  checked(f,var) BACKGROUND IF isGroup(var) THEN groupBackColor(3) // #f2f2f2
    PROPERTIES READONLY pos(var),name(var),tabName(var),canonicalName(var), isGroup(var)
    ORDERS pos(var), canonicalName(var), isGroup(var) DESC
   // FILTERS NOT isRoot(var)
    ;
    
    DESIGN taskTune { // расширяем дизайн формы Item
        PROPERTY (tabName(var)) { charWidth = 35; charHeight = 1; }
        PROPERTY (name(var)) { hide = TRUE; }
        PROPERTY (isGroup(var)) { hide = TRUE; }
        PROPERTY (canonicalName(var)) { hide = TRUE; } 
        PROPERTY (pos(var)) { charWidth = 5; flex = 1; } 
        details {
            MOVE BOX(var){
                  showIf = NOT isGroup(f);
            }
        }
    };
    
END

META configDimWOGroup(ObjectDim)

EXTEND CLASS  ObjectDim{
    empty '<>'
}
 
pos 'Порядок' = DATA INTEGER  (ObjectDim) IN base CHARWIDTH 5;
code 'Код' = DATA STRING[15] (ObjectDim) IN id;
name 'Наименование' = DATA ISTRING[254] (ObjectDim) IN id INDEXED ;
uuidInit 'Первоначальный uuid' = DATA ISTRING[36] (ObjectDim) IN id INDEXED ;
isRoot 'корень' = DATA BOOLEAN (ObjectDim);
isHide 'Скрыт' = DATA BOOLEAN (ObjectDim);
isGroup 'Е' = DATA BOOLEAN (ObjectDim);
getRoot##ObjectDim()   = GROUP MAX ObjectDim c IF isRoot(c) AND  NOT isGroup(c);
getEmpty##ObjectDim() = GROUP MAX ObjectDim c IF isRoot(c) AND isGroup(c);
 

WHEN LOCAL CHANGED(isRoot(ObjectDim sd))  AND ( (GROUP SUM(1) IF (isRoot(ObjectDim all) AND NOT isGroup(all) AND all IS ObjectDim)) > 1 ) DO {
   isRoot(sd)<-NULL ;
   MESSAGE 'только один элемент может представлять корень';
};

EXTEND CLASS  DimRegisters{
    ObjectDim ObjectDim
}

// Для настроек отчета
TABLE bSel###ObjectDim(ObjectDim, BReportConfig);
bSel 'Отбор' = DATA LOCAL BOOLEAN (ObjectDim, BReportConfig)  TABLE bSel###ObjectDim; // множество выбранных элементов для отчета

TABLE lBrepSel###ObjectDim(ObjectDim, BReportPartitionFilter);
lBrepSel = DATA BOOLEAN (ObjectDim, BReportPartitionFilter) TABLE lBrepSel###ObjectDim;

FORM listBreportSelected##ObjectDim 'Список' // toDo - Сделать иерархию
    OBJECTS  c = BReportPartitionFilter PANEL,
             s = ObjectDim
    PROPERTIES lBrepSel(s, c) DRAW s GRID,
        name 'Наименование' = name(s) READONLY 
    FILTERGROUP selOnly FILTER 'выбр/все' lBrepSel(s, c) DEFAULT
;

DESIGN listBreportSelected##ObjectDim {
    BOX { size = (600,400);}
}


    
FORM  form##ObjectDim##Selectlist  'Выбор элемента'
    OBJECTS  d = ObjectDim  
    PROPERTIES(d)  READONLY  
    ON CONTEXTMENU 'скрыть элемент вкл/выкл' {isHide(d) <- NOT isHide(d);}
    BACKGROUND  #e3e3e3 IF isHide(d) 
    pos, code, name 
    PROPERTIES(d) NEWEDIT,EDIT 
    
     // EVENTS ON INIT { EXPAND ALL TOP form##ObjectDim##TreeList.t; }
    FILTERS NOT (isRoot(d) AND NOT isGroup(d)) 
    ORDERS  pos(d)
    FILTERGROUP hided FILTER 'Скрывать неиспользуемые' NOT isHide(d) DEFAULT
    LIST ObjectDim OBJECT d
    ;

DESIGN   form##ObjectDim##Selectlist { 
   PROPERTY  (name(d))  { charWidth=40; defaultCompare='=*';}
   PROPERTY  (pos(d))   { hide = TRUE ; }
   BOX { size = (1200,600);}
} ;      

delAll##ObjectDim 'удалить все'() {
 DELETE ObjectDim a WHERE a IS ObjectDim ;
};
// форма для редактирования списка - открывается только из меню
EXTEND FORM  form##ObjectDim##list  
OBJECTS d = ObjectDim 
//PROPERTIES delAll##ObjectDim()
PROPERTIES(d)   isRoot,isGroup,isHide, NEW ,DELETE, 
               pos, code, name, uuidInit 
ORDERS  pos(d) 
; 
NAVIGATOR {
dimCatalogs   {
                 NEW form##ObjectDim##list;
              }
}  


current##ObjectDim = DATA LOCAL ObjectDim ();


newByName##ObjectDim##WoCheck(STRING name) {
  IF name AND NOT (name == '') THEN {
              NEW  new =  ObjectDim  {
                  name(new) <- name;
                  current##ObjectDim() <- new;
              }
  }  
}     


newByName##ObjectDim(STRING name) {
 IF name AND NOT (name == '') THEN {
    current##ObjectDim() <- GROUP MAX ObjectDim d IF name(d) == name;
    IF (NOT current##ObjectDim())   THEN {
              NEW  new =  ObjectDim  {
                  name(new) <- name;
                  current##ObjectDim() <- new;
           }
    }  
 }
 ELSE current##ObjectDim() <- NULL;
}       




importStr###ObjectDim = DATA LOCAL STRING (INTEGER); 
importUuidStr###ObjectDim = DATA LOCAL STRING[36] (INTEGER);       
import###ObjectDim    = DATA LOCAL ##ObjectDim(INTEGER);       
 
newByImportIndex##ObjectDim(INTEGER n) {
 IF importStr###ObjectDim(n) AND length(importStr###ObjectDim(n)) > 0 THEN {
    current##ObjectDim() <- GROUP MAX ObjectDim d IF name(d) == importStr###ObjectDim(n);
    IF (NOT current##ObjectDim())   THEN {
              NEW  new =  ObjectDim  {
                  name(new) <- importStr###ObjectDim(n);
                  uuidInit(new) <- importUuidStr###ObjectDim(n);
                  current##ObjectDim() <- new;
           }
    }  
 }
 ELSE current##ObjectDim() <- NULL;
}    

syncByName##ObjectDim() {
    FOR import###ObjectDim(INTEGER i) DO import###ObjectDim(i) <- NULL; 
    FOR importStr###ObjectDim(INTEGER n) == '' DO  importStr###ObjectDim(n) <- NULL;
    FOR importStr###ObjectDim(INTEGER n) DO importStr###ObjectDim(n) <- trimDeltn(importStr###ObjectDim(n));
    // если указан uuid то сначала смотрим по нему существующие элементы, если было переименование в первичном источнике!!! 
    FOR [GROUP MAX importUuidStr###ObjectDim(INTEGER n) IF length(importUuidStr###ObjectDim(n)) == 36 BY n](INTEGER nm) 
        AND 
        uuidInit(##ObjectDim p) == importUuidStr###ObjectDim(nm) 
        AND
        trimDeltn( name(p) ) != importStr###ObjectDim(nm)
        DO {
      name(p) <- importStr###ObjectDim(nm);
    }
      // остальное заводим по имени!!!
    FOR [GROUP MAX importStr###ObjectDim(INTEGER n) BY importStr###ObjectDim(n)](STRING nm) AND NOT (GROUP MAX ##ObjectDim p IF trimDeltn(name(p)) == nm) DO {
        newByName##ObjectDim(nm);
    }
    // если захотели добавить UUID
    FOR [GROUP MAX importUuidStr###ObjectDim(INTEGER n) IF length(importUuidStr###ObjectDim(n)) == 36 BY n](INTEGER nm) 
        AND 
        NOT uuidInit(##ObjectDim p)  
        AND
        trimDeltn( name(p) )== importStr###ObjectDim(nm)
        DO {
             uuidInit(p) <- importUuidStr###ObjectDim(nm);
    }
  
    // синхронизируем по имени
    FOR importStr###ObjectDim(INTEGER n) = trimDeltn (name(##ObjectDim p)) DO {
        import###ObjectDim(n) <- p;
    }
}                
    
    // Категории
   categoryValueName(Object p) += WHEN p IS ##ObjectDim THEN name(p AS ##ObjectDim);
   categoryValueClass(Object p) += WHEN p IS ##ObjectDim THEN ''##ObjectDim;
 // функция входимости объекта
objectInCategory 'функция входимости объекта в категорию' (DimCategoryItemsNamed n, ##ObjectDim o) = 
    GROUP MAX categoryGroup(DimCategory dk, ##ObjectDim o_) IF itemsName(dk) == n  AND o_ IS ##ObjectDim BY o_;
    
    
objectExistInCategory 'Есть ли в категории такой объект' (DimCategory n, ##ObjectDim o) = 
    GROUP MAX TRUE  IF categoryGroup(DimCategory dk, ##ObjectDim o_) AND dk == n  AND o_ IS ##ObjectDim;
    
objectInCategoryOrEpmty 'функция входимости объекта в категорию' (DimCategory n, ##ObjectDim o) = 
    OVERRIDE NOT objectExistInCategory(n, o),
    (GROUP MAX TRUE IF categoryGroup(DimCategory dk, ##ObjectDim o_) AND dk == n  AND o_ IS ##ObjectDim AND o_ == o);       
    
  EXTEND FORM  form##ObjectDim##list
  OBJECTS catg = DimCategory PANEL 
  PROPERTIES PANEL nameCat 'Категория' =  codeCt(catg) SELECTOR 
  PROPERTIES  
    inCatGroup 'вкл' = categoryGroup(catg, d)   
    HEADER  codeCt(GROUP MAX  DimCategory a_ IF [VIEW form##ObjectDim##list.catg](a_))  
    DRAW d GRID FIRST;
    
   DESIGN form##ObjectDim##list {
       PROPERTY (inCatGroup) {charWidth = 5;};
      
      TOOLBARRIGHT (d) {
          MOVE PROPERTY (nameCat){charWidth = 10;};
      }
      
   }
END

META configDimWO(ObjectDim)


importStr###ObjectDim = DATA LOCAL STRING (INTEGER);    
importStrCode###ObjectDim = DATA LOCAL STRING (INTEGER);    
importUuidStr###ObjectDim = DATA LOCAL STRING[36] (INTEGER);  
import###ObjectDim    = DATA LOCAL ##ObjectDim(INTEGER);       

syncByName##ObjectDim() ABSTRACT;
    
CLASS  ObjectDim##Named{
  empty ''
}
EXTEND CLASS  ObjectDim{
  empty '<>'
}


name(ObjectDim##Named obj_) = staticCaption(obj_); 

itemsCode 'Код элемента' (ObjectDim##Named obj) =  regexpReplace(staticName(obj),'.*\\.','','');

FORM form###ObjectDim##Namedlist
    OBJECTS d = ObjectDim##Named
    PROPERTIES name(d)
    LIST ObjectDim##Named OBJECT d
;

pos 'Порядок' = DATA INTEGER  (ObjectDim) IN base CHARWIDTH 5;
code 'Код' = DATA STRING[15] (ObjectDim) IN id INDEXED; 
name 'Наименование' = DATA ISTRING[254] (ObjectDim) IN id INDEXED ;
uuidInit 'Первоначальный uuid' = DATA ISTRING[36] (ObjectDim) IN id INDEXED ;
nameWoDuble(ObjectDim s) =  regexpReplace(name(s),'"','','g'); 
isGroup 'Группа' = DATA BOOLEAN (ObjectDim);
backgroundColor(ObjectDim s) = IF isGroup(s) THEN groupBackColor(3) ;
isHide 'Скрыт' = DATA BOOLEAN (ObjectDim);
parent 'Родитель' = DATA ObjectDim(ObjectDim) INDEXED;
//parentRoot 'Родитель корень' =  GROUP LAST ObjectDim d_ IF itemsName(d_) == obj;
nodeKey(ObjectDim obj_) = PARTITION SUM 1 IF obj_ IS ObjectDim ORDER obj_ MATERIALIZED; 
parentKey(ObjectDim obj_) = nodeKey(parent(obj_)) MATERIALIZED;

isRoot 'корень' = DATA BOOLEAN (ObjectDim);
itemsName 'Имя для формул' = DATA ObjectDim##Named(ObjectDim); 
    
CONSTRAINT name(ObjectDim d_) AND ObjectDim.empty == d_ MESSAGE 'Наименование корневого элемента не редактируется';

get##ObjectDim##ByItem(ObjectDim##Named obj) = GROUP MAX ObjectDim d_ IF itemsName(d_) == obj;
itemsCode 'Код элемента' (ObjectDim obj) =  regexpReplace(staticName(itemsName(obj)),'.*\\.','','');

level 'Уровень' (ObjectDim child, ObjectDim parent) =
   RECURSION 1l IF child IS ObjectDim AND parent = child
        STEP 2l IF parent = parent($parent) MATERIALIZED ;
        
levelInt 'Уровень' (ObjectDim child, ObjectDim parent) =
   RECURSION 1l IF child IS ObjectDim AND parent = child STEP 1l IF parent = parent($parent) MATERIALIZED;   
        
isParent 'Родитель' (ObjectDim child, ObjectDim parent) = TRUE IF levelInt(child, parent) MATERIALIZED;
        
canonicalName 'Каноническое имя' (ObjectDim group) =  GROUP CONCAT name(ObjectDim parent), ' / ' IF  level(group, parent) AND (group != parent OR isGroup(group)) ORDER DESC level(group, parent) CHARWIDTH 50;

renumPos##ObjectDim(ObjectDim d_) ABSTRACT;


mainOrder 'nodeKey'  (ObjectDim group) = //GROUP SUM 1 IF canonicalName(group) > canonicalName(ObjectDim group_)
nodeKey(group)
//STRING[9](
//ltrim(toChar((GROUP SUM 1 IF canonicalName(group) > canonicalName(ObjectDim group_)),'000000000'))
//) 
// INDEXED 
CHARWIDTH 9;

//levelNum 'levelNum' (ObjectDim group) =            (GROUP  MAX level(group, parent))  CHARWIDTH 2;
levelNumInt 'levelNumInt' (ObjectDim group) = LONG (GROUP  SUM levelInt(group,ObjectDim parent))-1  CHARWIDTH 2; // от 0 
maxLevelInt 'Максимальный уровень' (ObjectDim c, INTEGER maxLevel) = IF maxLevel > 0 THEN maxLevel ELSE (GROUP MAX levelNumInt(ObjectDim all_))+1; //для чего?
maxLevelInt 'Максимальный уровень' (ObjectDim c) =  (GROUP MAX levelNumInt(ObjectDim all_));


getLevelObj(ObjectDim ch, INTEGER lvl )          =  (GROUP MAX ObjectDim parent AS  ObjectDim IF  levelInt(ch,parent)                      AND levelNumInt(parent) == lvl);
getLevelObjWoItems(ObjectDim ch, INTEGER lvl )   =  (GROUP MAX ObjectDim parent AS  ObjectDim IF  levelInt(ch,parent) AND NOT ch == parent AND levelNumInt(parent) == lvl);


 
nameOrderPos (ObjectDim d) =  100000 (+) (PARTITION SUM 1 ORDER  (OVERRIDE pos(d),9999999), upper(name(d)));
nameOrderFull 'по алфавиту' (ObjectDim d) = GROUP CONCAT trim(toChar(nameOrderPos(ObjectDim dd),'000000')) IF isParent(d, dd),'' ORDER levelNumInt(dd) MATERIALIZED;


tabName 'Иерархия' (ObjectDim group) =  ISTRING((GROUP CONCAT '  ','  ' ORDER DESC levelInt(group, parent)) + name(group)) CHARWIDTH 15;
tabNameTabbed 'имя' (ObjectDim group) =  (GROUP CONCAT '\t','\t' ORDER DESC levelInt(group, parent)) + name(group) CHARWIDTH 15;
getRoot##ObjectDim()  = GROUP MAX ObjectDim c IF isRoot(c) AND NOT isGroup(c);
getEmpty##ObjectDim() = GROUP MAX ObjectDim c IF isRoot(c) AND isGroup(c);
WHEN LOCAL CHANGED(isRoot(ObjectDim sd))  AND ( (GROUP SUM(1) IF (isRoot(ObjectDim all) AND NOT isGroup(all)  AND all IS ObjectDim)) > 1 ) DO {
   isRoot(sd)<-NULL ;
   MESSAGE 'только один элемент может представлять корень';
};
// добавим материализированные поля родителей для lookup - хранение дубликатов имен в справочниках небольшая цена
// пока 3 уровня - 0-2 родителей 
name0L 'Группа урвн.0' (ObjectDim s)            = name(getLevelObj(s,0)) MATERIALIZED;
name0LOrderFull 'Группа,код урвн.1'  (ObjectDim s)  = nameOrderFull(getLevelObj(s,0)) MATERIALIZED;
name1L 'Группа урвн.1' (ObjectDim s)            = name(getLevelObj(s,1)) MATERIALIZED;
name1LOrderFull 'Группа,код урвн.1'  (ObjectDim s)  = nameOrderFull(getLevelObj(s,1)) MATERIALIZED;
name2L 'Группа урвн.2' (ObjectDim s)            = name(getLevelObj(s,2)) MATERIALIZED;
name2LOrderFull 'Группа,код урвн.2'  (ObjectDim s)  = nameOrderFull(getLevelObj(s,2)) MATERIALIZED;
name3L 'Группа урвн.3' (ObjectDim s)            = name(getLevelObj(s,3)) MATERIALIZED;
name3LOrderFull 'Группа,код урвн.3'  (ObjectDim s)  = nameOrderFull(getLevelObj(s,3)) MATERIALIZED;
name4L 'Группа урвн.4' (ObjectDim s)            = name(getLevelObj(s,4)) MATERIALIZED;
name4LOrderFull 'Группа,код урвн.4'  (ObjectDim s)  = nameOrderFull(getLevelObj(s,4)) MATERIALIZED;

// 


countDuble##ObjectDim(STRING name)   =  GROUP SUM 1 BY name(ObjectDim nn);


EXTEND CLASS  DimRegisters{
    ObjectDim ObjectDim
}

// Для настроек отчета
TABLE bSel###ObjectDim(ObjectDim, BReportConfig);
bSel 'Отбор' = DATA LOCAL BOOLEAN (ObjectDim, BReportConfig)  TABLE bSel###ObjectDim; // множество выбранных элементов для отчета



TABLE lBrepSel###ObjectDim(ObjectDim, BReportPartitionFilter);
lBrepSel = DATA BOOLEAN (ObjectDim, BReportPartitionFilter) TABLE lBrepSel###ObjectDim ;

FORM listBreportSelected###ObjectDim 'Список'  
    OBJECTS c = BReportPartitionFilter PANEL
    TREE   groupTree g = ObjectDim PARENT parent(g)
    OBJECTS          s = ObjectDim,
                  sSel 'ВЫБРАНО' = ObjectDim
      
    PROPERTIES READONLY nameG = IF isRoot(g) THEN ' ВСЁ' ELSE name(g)
    ORDERS nameG
    FILTERS isGroup(g) OR isRoot(g)
    
    PROPERTIES  BACKGROUND backgroundColor(s)
       lBrepSel(s, c) DRAW s GRID,
       nameS 'Наименование' = name(s) READONLY,
       isGroup(s)
    ORDERS  isGroup(s) DESC, nameS
    FILTERS isRoot(g) OR isParent(s, g)
    //FILTERS NOT isGroup(s)
    
    PROPERTIES BACKGROUND backgroundColor(sSel)
       lBrepSel(sSel, c) DRAW sSel GRID,
       name  'Наименование' = name(sSel) READONLY
    ORDERS name
    FILTERS lBrepSel(sSel, c)  
    
    ON INIT {
        SEEK listBreportSelected###ObjectDim.s = sSel;
    }
;

DESIGN listBreportSelected##ObjectDim {
    PROPERTY (nameG){charWidth = 10; fill = 100;}
    PROPERTY (isGroup(s)){ hide = TRUE;}
    OBJECTS {
        horizontal = TRUE;
        NEW hPan {
            MOVE BOX(TREE groupTree) {
            };
            GRID(TREE groupTree){
                
                hierarchicalWidth = 60;
                hierarchicalCaption = '';
            }
            fill = 0.25;
        }
        NEW bPan {
          fill = 1;
          MOVE BOX(s){
              TOOLBARSYSTEM(s) {
                  showViews = FALSE;
                  showSettings = FALSE;
                  //showFilters = FALSE;
                  showPrintGroupXls = FALSE;
                  showCountQuantity = FALSE;
                  showCalculateSum = FALSE;
                  showManualUpdate = FALSE;
              }
          };
          MOVE BOX(sSel) {
              fill = 0.3;
              TOOLBARSYSTEM(sSel){
                  showViews = FALSE;
                  showSettings = FALSE;
                  showFilters = FALSE;
                  showPrintGroupXls = FALSE;
                  showCountQuantity = FALSE;
                  showCalculateSum = FALSE;
                  showManualUpdate = FALSE;
              };
          }; 
        }
    }
    
    BOX { size = (800,600);}
}
// настроки отчета

// список для выбора группы 
EXTEND FORM form##ObjectDim##Group  
OBJECTS d = ObjectDim 
PROPERTIES(d) 
ON CONTEXTMENU 'скрыть элемент вкл/выкл' {isHide(d) <- NOT isHide(d);}
BACKGROUND  #e3e3e3 IF isHide(d)
READONLY pos, code, canonicalName, nameOrderFull
ORDERS pos(d), nameOrderFull(d) FILTERS isGroup(d) AND NOT isRoot(d)
FILTERGROUP hided FILTER 'Скрывать неиспользуемые' NOT isHide(d) DEFAULT;

selectGroup(ObjectDim r) {
 DIALOG form##ObjectDim##Group OBJECTS d = parent(r) CHANGE;
};
     
EXTEND  FORM  form##ObjectDim##TreeList  
TREE tree##ObjectDim t = ObjectDim  PARENT parent(t)
PROPERTIES (t)     
    ON CONTEXTMENU 'скрыть элемент вкл/выкл' {isHide(t) <- NOT isHide(t);}
    BACKGROUND  #e3e3e3 IF isHide(t)
       
        tabName  BACKGROUND backgroundColor(t)  
        ,pos
EVENTS ON INIT {EXPAND ALL TOP form##ObjectDim##TreeList.t;} 
FILTERS NOT (isRoot(t) AND isGroup(t)) 
ORDERS  pos(t),tabName(t) 
FILTERGROUP hided FILTER 'Скрывать неиспользуемые' NOT isHide(t) DEFAULT
//LIST ObjectDim OBJECT t
;
DESIGN   form##ObjectDim##TreeList { 
    PROPERTY (tabName(t)) { charWidth=100; charHeight = 1; }
} ;



         
                         
FORM form##ObjectDim##Selectlist 'Выбор элемента' 
    OBJECTS  d = ObjectDim  
    PROPERTIES(d)  READONLY 
    ON CONTEXTMENU 'скрыть элемент вкл/выкл' {isHide(d) <- NOT isHide(d);}
    BACKGROUND  #e3e3e3 IF isHide(d)
    code, name, tabName  BACKGROUND backgroundColor(d)  BACKGROUND backgroundColor(d)   
    ,pos  ,nameOrderFull, isGroup
    PROPERTIES(d) NEWEDIT,EDIT
    PROPERTIES itemsName 'Имя для формул' = name(itemsName(d)) 
    FILTERS   NOT (isRoot(d) AND isGroup(d)) 
    ORDERS  pos(d), nameOrderFull(d), isGroup(d) DESC
    FILTERGROUP hided FILTER 'Скрывать неиспользуемые' NOT isHide(d) DEFAULT
    LIST ObjectDim OBJECT d
    ;

DESIGN   form##ObjectDim##Selectlist { 
   PROPERTY  (tabName(d))   { charWidth=60; charHeight = 1; }
   PROPERTY  (pos(d))   { hide = TRUE ; }
   PROPERTY  (isGroup(d))   { hide = TRUE ; }
   PROPERTY (nameOrderFull(d)) {charWidth = 3; }
   PROPERTY (itemsName) {charWidth=10;}
  BOX { size = (1200,600);}
} ;




     
      // форма для редактирования списка - открывается только из меню
       EXTEND FORM  form##ObjectDim##list  
       OBJECTS d = ObjectDim 
  //     PROPERTIES delAll##ObjectDim()
       PROPERTIES mainOrder(d),levelNumInt(d) 
       PROPERTIES(d)   isRoot,NEW ,DELETE, 
                       isGroup BACKGROUND backgroundColor(d) STICKY,
                       isHide BACKGROUND backgroundColor(d),
                     //  levelNumInt BACKGROUND backgroundColor(d), 
                       
                       pos BACKGROUND backgroundColor(d)
                       ON CONTEXTMENU 'Перенумеровать по иерархии от ...' {INPUT   inp = pos(d) DO {
                                                                               LOCAL n_ = INTEGER();
                                                                               n_() <- inp;
                                                                               FOR isParent(ObjectDim d_, d) ORDER canonicalName(d_)  DO {
                                                                                   pos(d_) <- n_();
                                                                                   n_() <- n_() + 2;
                                                                               }
                                                                           }
                                                                          }, 
                       code BACKGROUND backgroundColor(d), 
                       tabName BACKGROUND backgroundColor(d) STICKY, 
                       name BACKGROUND backgroundColor(d) 
       PROPERTIES      itemsName 'Имя для формул' = name(itemsName(d)),
                        parent 'Родитель' = name(parent(d)) ON CHANGE {DIALOG form##ObjectDim##TreeList  OBJECTS t = parent(d)  CHANGE = parent(d) LIST name(t) FILTERS isGroup(t); },
                         //selectGroup(d) BACKGROUND backgroundColor(d),
                       nameOrderFull(d) BACKGROUND backgroundColor(d),
                       uuidInit(d),
           name0L(d), name0LOrderFull (d), name1L(d), name1LOrderFull (d), name2L(d), name2LOrderFull (d), name3L(d), name3LOrderFull (d), name4L(d), name4LOrderFull(d)

       FILTERGROUP dubles FILTER 'Показать дубли' countDuble##ObjectDim(name(d)) > 1
       ORDERS  pos(d), nameOrderFull(d), isGroup(d) DESC 
       ; 
       DESIGN  form##ObjectDim##list {
           PROPERTY (name(d))     {charWidth=20;}
           PROPERTY (nameOrderFull(d))     {charWidth=2;}
           PROPERTY (tabName(d))  {charWidth=20; charHeight=1;}
           PROPERTY (parent)      {charWidth=20;}
           PROPERTY (uuidInit(d)) {charWidth=10;}
           PROPERTY (name0L(d)) {hide=TRUE;}
           PROPERTY (name0LOrderFull(d)) {hide=TRUE;}
           PROPERTY (name1L(d)) {hide=TRUE;}
           PROPERTY (name1LOrderFull(d)) {hide=TRUE;}           
           PROPERTY (name2L(d)) {hide=TRUE;}
           PROPERTY (name2LOrderFull(d)) {hide=TRUE;}           
           PROPERTY (name3L(d)) {hide=TRUE;}
           PROPERTY (name3LOrderFull(d)) {hide=TRUE;}           
           PROPERTY (name4L(d)) {hide=TRUE;}
           PROPERTY (name4LOrderFull(d)) {hide=TRUE;}
       }  
       NAVIGATOR {
                   dimCatalogs   {
                    NEW form##ObjectDim##list;
                   }
                 }
        
delAll##ObjectDim 'удалить видимое'() {
    DELETE ObjectDim a WHERE a IS ObjectDim AND [FILTER form##ObjectDim##list.d](a);
};
EXTEND FORM form##ObjectDim##list PROPERTIES delAll##ObjectDim() SHOWIF has(currentUser(), userRoleSID('dirAdmin'));
DESIGN  form##ObjectDim##list { 
    TOOLBARRIGHT {
        MOVE PROPERTY (delAll##ObjectDim());
    }
}




FORM  form##ObjectDim##Card
    OBJECTS c = ObjectDim PANEL
    PROPERTIES (c) name
    PROPERTIES  listOpen 'Открыть список' = {SHOW form##ObjectDim##list DOCKED NEWSESSION;}
    
    EDIT ObjectDim OBJECT c;
DESIGN form##ObjectDim##Card {
    TOOLBARRIGHT {MOVE PROPERTY (listOpen);}
}


current##ObjectDim = DATA LOCAL ObjectDim ();

newByName##ObjectDim(STRING name) {
 IF name AND NOT (name == '') THEN {
    current##ObjectDim() <- GROUP MAX ObjectDim d IF name(d) == name;
    IF (NOT current##ObjectDim())   THEN {
              NEW  new =  ObjectDim  {
                  name(new) <- name;
                  current##ObjectDim() <- new;
           }
    }  
 }
 ELSE current##ObjectDim() <- NULL;
}

newByCode##ObjectDim(STRING code) {
 IF code AND NOT (code == '') THEN {
    current##ObjectDim() <- GROUP MAX ObjectDim d IF code(d) == code;
    IF (NOT current##ObjectDim())   THEN {
              NEW  new =  ObjectDim  {
                  code(new) <- code;
                  current##ObjectDim() <- new;
           }
    }  
 }
 ELSE current##ObjectDim() <- NULL;
}
       
       
       
       
             

newByImportIndex##ObjectDim(INTEGER n) {
 IF importStr###ObjectDim(n) AND length(importStr###ObjectDim(n)) > 0 THEN {
    current##ObjectDim() <- GROUP MAX ObjectDim d IF name(d) == importStr###ObjectDim(n);
    IF (NOT current##ObjectDim())   THEN {
              NEW  new =  ObjectDim  {
                  name(new) <- importStr###ObjectDim(n);
                  uuidInit(new) <- importUuidStr###ObjectDim(n);
                  current##ObjectDim() <- new;
           }
    }  
 }
 ELSE current##ObjectDim() <- NULL;
}    

newByImportIndexCode##ObjectDim(INTEGER n) {
 IF importStrCode###ObjectDim(n) AND length(importStrCode###ObjectDim(n)) > 0 THEN {
    current##ObjectDim() <- GROUP MAX ObjectDim d IF code(d) == importStrCode###ObjectDim(n);
    IF (NOT current##ObjectDim())   THEN {
              NEW  new =  ObjectDim  {
                  name(new) <- importStr###ObjectDim(n);
                  code(new) <- importStrCode###ObjectDim(n);
                  uuidInit(new) <- importUuidStr###ObjectDim(n);
                  current##ObjectDim() <- new;
           }
    }  
 }
 ELSE current##ObjectDim() <- NULL;
}    
       
syncByName##ObjectDim()+{
    FOR import###ObjectDim(INTEGER i) DO import###ObjectDim(i) <- NULL;
    FOR importStr###ObjectDim(INTEGER n) == '' DO  importStr###ObjectDim(n) <- NULL;
    FOR importStr###ObjectDim(INTEGER n) DO importStr###ObjectDim(n) <- trimDeltn(importStr###ObjectDim(n));
    // если указан uuid то сначала смотрим по нему существующие элементы, если было переименование в первичном источнике!!! 
    FOR [GROUP MAX importUuidStr###ObjectDim(INTEGER n) IF length(importUuidStr###ObjectDim(n)) == 36 BY n](INTEGER nm) 
        AND 
        uuidInit(##ObjectDim p) == importUuidStr###ObjectDim(nm) 
        AND 
        NOT trimDeltn(name(p)) == importStr###ObjectDim(nm)
        DO {
             name(p) <- importStr###ObjectDim(nm);
    }
    
       
    // остальное заводим по имени!!!
    FOR [GROUP MAX importStr###ObjectDim(INTEGER n) BY importStr###ObjectDim(n)](STRING nm) AND NOT (GROUP MAX ##ObjectDim p IF trimDeltn(name(p)) == nm)   
         NEW  new =  ObjectDim DO { 
            name(new) <- nm; 
    }
    
    // если захотели добавить UUID
    FOR [GROUP MAX importUuidStr###ObjectDim(INTEGER n) IF length(importUuidStr###ObjectDim(n)) == 36 BY n](INTEGER nm) 
        AND 
        NOT uuidInit(##ObjectDim p)  
        AND
        trimDeltn(name(p)) == importStr###ObjectDim(nm)
        DO {
             uuidInit(p) <- importUuidStr###ObjectDim(nm);
    }
 
    // синхронизируем по имени
    FOR importStr###ObjectDim(INTEGER n) = trimDeltn( name(##ObjectDim p) ) DO {
        import###ObjectDim(n) <- p;
    }
}                       
 
syncByCode##ObjectDim() {
    FOR import###ObjectDim(INTEGER i) DO import###ObjectDim(i) <- NULL;
    // если указан uuid то сначала смотрим по нему существующие элементы, если было переименование в первичном источнике!!! 
//    FOR [GROUP MAX importUuidStr###ObjectDim(INTEGER n) IF length(importUuidStr###ObjectDim(n)) == 36 BY n](INTEGER nm) 
//        AND 
//        uuidInit(##ObjectDim p) == importUuidStr###ObjectDim(nm) 
//        AND 
//        NOT name(p) == importStr###ObjectDim(nm)  
//        DO {
//             name(p) <- importStr###ObjectDim(nm);
//    }
   FOR [GROUP MAX importUuidStr###ObjectDim(INTEGER n) IF length(importUuidStr###ObjectDim(n)) == 36 BY n](INTEGER nm) 
        AND 
        uuidInit(##ObjectDim p) == importUuidStr###ObjectDim(nm) 
        AND 
         NOT code(p) == importStrCode###ObjectDim(nm) 
        DO {
             code(p) <- importStrCode###ObjectDim(nm);
    }
           
    // остальное заводим по коду!!!
    FOR [GROUP MAX importStrCode###ObjectDim(INTEGER n) BY importStrCode###ObjectDim(n)](STRING nm) AND NOT (GROUP MAX ##ObjectDim p IF code(p) == nm)   
        NEW  new =  ObjectDim DO { 
        code(new) <- nm; 
    }
    //  добавить Имя в новые
     FOR [GROUP MAX importStr###ObjectDim(INTEGER n) IF length(importStr###ObjectDim(n)) > 0 BY n](INTEGER nm) 
        AND code(##ObjectDim  p) == importStrCode###ObjectDim(nm)
        AND NOT trimDeltn(name(p)) == importStr###ObjectDim(nm) 
        DO {
             name(p) <- importStr###ObjectDim(nm);
    }
    
    //  добавить UUID
    FOR [GROUP MAX importUuidStr###ObjectDim(INTEGER n) IF length(importUuidStr###ObjectDim(n)) == 36 BY n](INTEGER nm) 
        AND code(##ObjectDim  p) == importStrCode###ObjectDim(nm)
        AND NOT uuidInit(p)  
        DO {
             uuidInit(p) <- importUuidStr###ObjectDim(nm);
    }
 
    // синхронизируем по Коду
    FOR importStrCode###ObjectDim(INTEGER n) = code(##ObjectDim p) DO {
        import###ObjectDim(n) <- p;
    }
}          
       

syncByUuid##ObjectDim() {
    FOR import###ObjectDim(INTEGER i) DO import###ObjectDim(i) <- NULL;
    FOR importStr###ObjectDim(INTEGER n) DO importStr###ObjectDim(n) <- trimDeltn(importStr###ObjectDim(n));
           
    // остальное заводим по коду!!!
    FOR [GROUP MAX importUuidStr###ObjectDim(INTEGER n) BY importUuidStr###ObjectDim(n)](STRING nm) AND NOT (GROUP MAX ##ObjectDim p IF uuidInit(p) == nm)   
        NEW  new =  ObjectDim DO { 
        uuidInit(new) <- nm; 
    }
    //  добавить Имя в новые
     FOR [GROUP MAX importStr###ObjectDim(INTEGER n) IF length(importStr###ObjectDim(n)) > 0 BY n](INTEGER nm) 
        AND uuidInit(##ObjectDim  p) == importUuidStr###ObjectDim(nm)
        AND NOT trimDeltn(name(p)) == importStr###ObjectDim(nm) 
        DO {
             name(p) <- importStr###ObjectDim(nm);
    }
      FOR [GROUP MAX importStrCode###ObjectDim(INTEGER n) IF length(importStrCode###ObjectDim(n)) > 0 BY n](INTEGER nm) 
        AND uuidInit(##ObjectDim  p) == importUuidStr###ObjectDim(nm)
        AND NOT code(p) == importStrCode###ObjectDim(nm) 
        DO {
             code(p) <- importStrCode###ObjectDim(nm);
    }
    
 
    // синхронизируем по Коду
    FOR importUuidStr###ObjectDim(INTEGER n) = uuidInit(##ObjectDim p) DO {
        import###ObjectDim(n) <- p;
    }
}                 
       
       
// Категории
   categoryValueName(Object p) += WHEN p IS ##ObjectDim THEN name(p AS ##ObjectDim);
   categoryValueClass(Object p) += WHEN p IS ##ObjectDim THEN ''##ObjectDim;
 // функция входимости объекта
objectInCategory 'функция входимости объекта в категорию' (DimCategoryItemsNamed n, ##ObjectDim o) = 
    GROUP MAX categoryGroup(DimCategory dk, ##ObjectDim o_) IF itemsName(dk) == n  AND o_ IS ##ObjectDim BY o_;

objectInCategory 'функция входимости объекта в категорию' (DimCategory n, ##ObjectDim o) = 
    GROUP MAX categoryGroup(DimCategory dk, ##ObjectDim o_) IF dk == n  AND o_ IS ##ObjectDim BY o_;       
       
objectExistInCategory 'Есть ли в категории такой объект' (DimCategory n, ##ObjectDim o) = 
    GROUP MAX TRUE  IF categoryGroup(DimCategory dk, ##ObjectDim o_) AND dk == n  AND o_ IS ##ObjectDim;
    
objectInCategoryOrEpmty 'функция входимости объекта в категорию' (DimCategory n, ##ObjectDim o) = 
    OVERRIDE NOT objectExistInCategory(n, o),
    (GROUP MAX TRUE IF categoryGroup(DimCategory dk, ##ObjectDim o_) AND dk == n  AND o_ IS ##ObjectDim AND o_ == o);       
    
    
    
  EXTEND FORM  form##ObjectDim##list
  OBJECTS catg = DimCategory PANEL 
  PROPERTIES PANEL nameCat 'Категория' =  codeCt(catg) SELECTOR 
  PROPERTIES  
    inCatGroup 'вкл' = categoryGroup(catg, d)   
    HEADER  codeCt(GROUP MAX  DimCategory a_ IF [VIEW form##ObjectDim##list.catg](a_))  
    DRAW d GRID FIRST;
    
   DESIGN form##ObjectDim##list {
       PROPERTY (inCatGroup) {charWidth = 5;};
      
      TOOLBARRIGHT (d) {
          MOVE PROPERTY (nameCat){charWidth = 10;};
      }
      
   }
    
END 
 
META configCategory(ObjectDim)
  
// Категории
   categoryValueName(Object p) += WHEN p IS ##ObjectDim THEN name[##ObjectDim](p AS ##ObjectDim);
   categoryValueClass(Object p) += WHEN p IS ##ObjectDim THEN ''##ObjectDim;
 // функция входимости объекта
objectInCategory 'функция входимости объекта в категорию' (DimCategoryItemsNamed n, ##ObjectDim o) = 
    GROUP MAX categoryGroup(DimCategory dk, ##ObjectDim o_) IF itemsName(dk) == n  AND o_ IS ##ObjectDim BY o_;

objectInCategory 'функция входимости объекта в категорию' (DimCategory n, ##ObjectDim o) = 
    GROUP MAX categoryGroup(DimCategory dk, ##ObjectDim o_) IF dk == n  AND o_ IS ##ObjectDim BY o_;       
       
objectExistInCategory 'Есть ли в категории такой объект' (DimCategory n, ##ObjectDim o) = 
    GROUP MAX TRUE  IF categoryGroup(DimCategory dk, ##ObjectDim o_) AND dk == n  AND o_ IS ##ObjectDim;
    
objectInCategoryOrEpmty 'функция входимости объекта в категорию' (DimCategory n, ##ObjectDim o) = 
    OVERRIDE NOT objectExistInCategory(n, o),
    (GROUP MAX TRUE IF categoryGroup(DimCategory dk, ##ObjectDim o_) AND dk == n  AND o_ IS ##ObjectDim AND o_ == o);       
      
  EXTEND FORM  form##ObjectDim##list
  OBJECTS catg = DimCategory PANEL 
  PROPERTIES PANEL nameCat 'Категория' =  codeCt(catg) SELECTOR 
  PROPERTIES  
    inCatGroup 'вкл' = categoryGroup(catg, d)   
    HEADER  codeCt(GROUP MAX  DimCategory a_ IF [VIEW form##ObjectDim##list.catg](a_))  
    DRAW d GRID FIRST;
    
  DESIGN form##ObjectDim##list {
      PROPERTY (inCatGroup) {charWidth = 5;};
      
      TOOLBARRIGHT (d) {
          MOVE PROPERTY (nameCat){charWidth = 10;};
      }
   }
  NAVIGATOR {
  dimCatalogs   {
                 NEW form##ObjectDim##list;
              }
}    
    
    
END 

@configCategory(DimCategory);

// Категория констант категорий
@configCategory(DimCategoryItemsNamed);
